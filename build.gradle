import org.apache.tools.ant.filters.ReplaceTokens


plugins {
    id 'java'
    // https://github.com/GradleUp/shadow
    id 'com.gradleup.shadow' version '8.3.7'
    // https://github.com/Minecrell/plugin-yml
    id 'net.minecrell.plugin-yml.bukkit' version '0.6.0'
}

group 'me.datatags.constanthunger'
version '6.3.1'

compileJava {
    sourceCompatibility = '21'
    targetCompatibility = '21'
    options.encoding = 'UTF-8'
}


repositories {
    mavenCentral()

    // Sonatype
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }
    // SpigotMC
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

dependencies {
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    implementation 'org.jetbrains:annotations:26.0.2'
    // https://github.com/dejvokep/boosted-yaml
    implementation 'dev.dejvokep:boosted-yaml:1.3.7'
    // https://github.com/Bastian/bstats-metrics
    implementation 'org.bstats:bstats-bukkit:3.1.0'
    // https://hub.spigotmc.org/stash/projects/SPIGOT/repos/spigot/browse
    compileOnly 'org.spigotmc:spigot-api:1.21.4-R0.1-SNAPSHOT'
}


shadowJar {
    archiveFileName = project.name + "-" + project.version + ".jar"
    relocate 'dev.dejvokep.boostedyaml', 'me.datatags.constanthunger.boostedyaml'
    relocate 'org.bstats', 'me.datatags.constanthunger.metrics'
    exclude 'META-INF/**', 'LICENSE'
}

build {
    dependsOn(shadowJar)
}


bukkit {
    name = 'ConstantHunger'
    main = 'me.datatags.constanthunger.ConstantHunger'
    description = 'Plugin that keeps hunger constant at all times'
    version = '@VERSION@'
    apiVersion = '1.13'
    authors = ['datatags', 'BySwiizen']
}

processResources {
    filter ReplaceTokens, tokens: ["VERSION" : project.version]
}