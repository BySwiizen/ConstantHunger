import org.apache.tools.ant.filters.ReplaceTokens


plugins {
    id 'java'
    // https://github.com/Goooler/shadow
    id 'io.github.goooler.shadow' version '8.1.7'
    // https://github.com/Minecrell/plugin-yml
    id 'net.minecrell.plugin-yml.bukkit' version '0.6.0'
}

group 'me.datatags.constanthunger'
version '5.1'

compileJava {
    sourceCompatibility = '21'
    targetCompatibility = '21'
    options.encoding = 'UTF-8'
}


repositories {
    mavenCentral()

    // Sonatype
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }
    // SpigotMC
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

dependencies {
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    implementation 'org.jetbrains:annotations:24.1.0'
    // https://github.com/dejvokep/boosted-yaml
    implementation 'dev.dejvokep:boosted-yaml:1.3.5'
    // https://github.com/Bastian/bstats-metrics
    implementation 'org.bstats:bstats-bukkit:3.0.2'
    // https://hub.spigotmc.org/stash/projects/SPIGOT/repos/spigot/browse
    compileOnly 'org.spigotmc:spigot-api:1.20.6-R0.1-SNAPSHOT'
}


shadowJar {
    archiveClassifier = null
    relocate 'org.bstats', 'me.datatags.constanthunger.metrics'
    relocate 'dev.dejvokep.boostedyaml', 'me.datatags.constanthunger.boostedyaml'
}

build {
    dependsOn(shadowJar)
}


bukkit {
    name = 'ConstantHunger'
    main = 'me.datatags.constanthunger.ConstantHunger'
    description = 'Plugin that keeps hunger constant at all times'
    version = '@VERSION@'
    apiVersion = '1.13'
    authors = ['datatags', 'BySwiizen']
}

processResources {
    inputs.property("version", project.version)

    filter ReplaceTokens, tokens: [
            'VERSION' : project.version,
    ]
}